#created on: Jul 7, 2011
package SessionProcessor

#list any import classes here.
import org.apache.usergrid.apm.model.ClientLog;
import org.apache.usergrid.apm.model.ClientSessionMetrics;
import org.apache.usergrid.apm.model.SummarySessionMetrics;
import org.apache.usergrid.apm.model.CompactSessionMetrics;
import org.apache.usergrid.apm.model.ChartCriteria;

import java.util.List;
import java.util.TreeSet;
import java.util.Date;
import org.apache.usergrid.apm.service.charts.service.SessionChartCriteriaService;
import org.apache.usergrid.apm.service.charts.service.NetworkMetricsChartCriteriaService;
import org.apache.usergrid.apm.service.charts.service.LogChartCriteriaService;

global org.apache.commons.logging.Log log;

global	List dirtySummarySessionMetrics;	
global	List dirtyCompactClientLog;
global	List dirtyCompactSessionMetrics;
global	List dirtyCompactNetworkMetrics;
		

global SessionChartCriteriaService sessionChartCriteriaService;
global NetworkMetricsChartCriteriaService networkMetricsChartCriteriaService;
global LogChartCriteriaService logChartCriteriaService;



global Long appId;

dialect "java"


declare DeviceModelType
	type: String
end

rule "DeviceModelTypeRule"
	no-loop true
when 
	$ssm : SummarySessionMetrics( $deviceModel : deviceModel ) from entry-point "summarySessionMetricsStream"
	not (exists DeviceModelType(type == $deviceModel))
then
	//For some reason, insertLogical does not work - NOTE: Fixed in Drools 5.2 <- Need to fix
	insert( new DeviceModelType($deviceModel));
end


/*
* Generates new csm whenever it sees a new $ssm
*/

rule "CompactSessionMetricsGenerator_SessionsByDeviceModel"
   salience -100
when
  $model : DeviceModelType();
  $minute : Long() from entry-point "modifiedMinutesStream"
then
  log.info("Detected new SummarySessionMetrics to trigger new CompactSessionMetricsGeneratorByDeviceModel");
  log.info("AppId : " + appId + "Model : " + $model.getType() + " SummarySession Start Minute : " + $minute);
  //log.info("SummarySession Timestamp : " + $ssm.getSessionStartTime() + " SummarySession Start Minute : " + $ssm.getStartMinute());  
  //log.info();
  CompactSessionMetrics csm = new CompactSessionMetrics();
  csm.setAppId(appId);
  csm.setTimeStamp(new Date( $minute * 1000 * 60));
  csm.setDeviceModel($model.getType());
  log.info("csm Timestamp : " + csm.getTimeStamp());
  Long id = 1L;
  id = sessionChartCriteriaService.getDefaultChartCriteriaByName( appId,SessionChartCriteriaService.ACTIVE_SESSIONS_BY_DEVICE_MODEL).getId();
  log.info("Setting Chart Criteria ID : " + id);  
  csm.setChartCriteriaId(id);
  csm.setSamplePeriod(ChartCriteria.SamplePeriod.MINUTE);
  entryPoints["compactSessionMetricsStream_SessionsByDeviceModel"].insert(csm);
end

rule "CompactSessionMetricsCalculator_SessionsByDeviceModel"
  salience -200
  no-loop true
when
 $model : DeviceModelType($type : type)
 $csm: CompactSessionMetrics($model.type == deviceModel, $end : endMinute) from entry-point "compactSessionMetricsStream_SessionsByDeviceModel"
 $sessionCount : Number() 
	from accumulate (SummarySessionMetrics(appId == $csm.appId, $sessionLength : sessionLength,
		deviceModel == $model.type, 
		$csm.endMinute >= prevEndMinute, $csm.endMinute < endMinute  ) 
		from entry-point "summarySessionMetricsStream", count(null))
 $sumSessionLength : Number() 
	from accumulate (SummarySessionMetrics(appId == $csm.appId, $sessionLength : sessionLength,
		deviceModel == $model.type , 
		$csm.endMinute >= prevEndMinute, $startMinute : startMinute, $csm.endMinute < endMinute) 
		from entry-point "summarySessionMetricsStream", sum($end - $startMinute))
then
  if($sessionCount != null && $sumSessionLength != null)
  {
    log.info("CompactSessionMetricsCalculator_SessionsByDeviceModel triggered. AppID : " + $csm.getAppId() + 
       "Model : " + $model.getType() + " Minute : " + $csm.getEndMinute() + " TS : " + $csm.getTimeStamp()); 
  	log.info("Count : " + $sessionCount.longValue());
  	log.info("SumSumLength in seconds : " + $sumSessionLength.longValue());
  	modify($csm){
  		setSessionCount($sessionCount.longValue()),
  		setSumSessionLength($sumSessionLength.longValue()*60*1000) //store in milliseconds
 	}
  }
end

rule "PersistModifiedCompactSessionMetrics_SessionsByDeviceModel"
   salience -500
when
 $metric : CompactSessionMetrics() from entry-point "compactSessionMetricsStream_SessionsByDeviceModel"
then
 log.info("Marking compactSessionMetricsStream_SessionsByDeviceModel dirty " + $metric.getId() + " Output " + $metric.toString());
 dirtyCompactSessionMetrics.add($metric);
end