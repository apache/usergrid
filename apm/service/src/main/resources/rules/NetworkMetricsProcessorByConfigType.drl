#created on: Apr 14, 2011
package NetworkMetricsProcessor

import org.apache.usergrid.apm.model.*;
import org.apache.usergrid.apm.model.*;
import java.util.*;
import org.apache.usergrid.apm.service.charts.service.SessionChartCriteriaService;
import org.apache.usergrid.apm.service.charts.service.NetworkMetricsChartCriteriaService;
import org.apache.usergrid.apm.service.charts.service.LogChartCriteriaService;


global org.apache.commons.logging.Log log;

global	List dirtySummarySessionMetrics;	
global	List dirtyCompactClientLog;
global	List dirtyCompactSessionMetrics;
global	List dirtyCompactNetworkMetrics;

global SessionChartCriteriaService sessionChartCriteriaService;
global NetworkMetricsChartCriteriaService networkMetricsChartCriteriaService;
global LogChartCriteriaService logChartCriteriaService;

dialect "java"

declare NetworkMetricsConfigType
	type: String
end

rule "NetworkMetricsConfigTypeRule"
	no-loop true
when 
	$nm : ClientNetworkMetrics( $appConfigType : appConfigType ) from entry-point "clientNetworkMetricsStream"
	not (exists NetworkMetricsConfigType(type == $appConfigType))
then	
	insert( new NetworkMetricsConfigType($appConfigType));
end



rule "CompactNetworkMetricConfigTypeGenerator"
when
 $appConfigType : NetworkMetricsConfigType()
 $nm : ClientNetworkMetrics(endMinute != null, appConfigType == $appConfigTpe.type) from entry-point "clientNetworkMetricsStream"
 not (exists (CompactNetworkMetrics(appId == $nm.appId,endMinute == $nm.endMinute,
     appConfigType ==$appConfigType.type) from entry-point "compactNetworkMetricsStream_ByConfigType"))
then
 log.info("Detected new ClientNetworkMetrics to trigger new CompactNetworkMetricsConfigTypeGenerator : " +  $nm.getEndMinute() + " AppID: " + $nm.getAppId()); 
   
 CompactNetworkMetrics metric = new CompactNetworkMetrics();
 metric.setTimeStamp(new Date( $nm.getEndMinute() * 1000 * 60));
 metric.setAppConfigType($appConfigType.getType());
 metric.setAppId($nm.getAppId());
 metric.setSamplePeriod(ChartCriteria.SamplePeriod.MINUTE);
 Long id = networkMetricsChartCriteriaService.getDefaultChartCriteriaByName( $nm.getAppId(), NetworkMetricsChartCriteriaService.NETWORK_ERRORS_BY_APP_CONFIG).getId();
 metric.setChartCriteriaId(id);
 
 entryPoints["compactNetworkMetricsStream_ByConfigType"].insert(metric);
end


rule "CompactNetworkMetricsConfigTypeCalculator"
   salience -200
   no-loop true
when
 $metric : CompactNetworkMetrics() from entry-point "compactNetworkMetricsStream_ByConfigType" 
 $sumNumSamples : Number() 
	from accumulate (ClientNetworkMetrics(appId == $metric.appId, endMinute == $metric.endMinute, appConfigType == $metric.appConfigType, $numSamples : numSamples )
		from entry-point "clientNetworkMetricsStream" , sum($numSamples))
 $sumNumErrors : Number() 
	from accumulate (ClientNetworkMetrics(appId == $metric.appId, endMinute == $metric.endMinute, appConfigType == $metric.appConfigType,$numErrors : numErrors )
		from entry-point "clientNetworkMetricsStream" , sum($numErrors))
 $sumSumLatency : Number() 
	from accumulate (ClientNetworkMetrics(appId == $metric.appId, endMinute == $metric.endMinute, appConfigType == $metric.appConfigType,$sumLatency : sumLatency )
		from entry-point "clientNetworkMetricsStream" , sum($sumLatency))
 $minMinLatency : Number() 
	from accumulate (ClientNetworkMetrics(appId == $metric.appId, endMinute == $metric.endMinute, appConfigType == $metric.appConfigType,$minLatency : minLatency )
		from entry-point "clientNetworkMetricsStream" , min($minLatency))
 $maxMaxLatency : Number() 
	from accumulate (ClientNetworkMetrics(appId == $metric.appId, endMinute == $metric.endMinute, appConfigType == $metric.appConfigType,$maxLatency : maxLatency )
		from entry-point "clientNetworkMetricsStream" , max($maxLatency))		
then

 log.info("CompactNetworkMetricsCalculator : $numSamples | $numErrors |  $sumLatency | $minLatency | $maxLatency "
 	+ $sumNumSamples + "|" 
 	+ $sumNumErrors + "|" 
 	+ $sumSumLatency + "|" 
 	+ $minMinLatency + "|" 
 	+ $maxMaxLatency );

 modify($metric) {
 	setNumSamples(new Long($sumNumSamples.longValue())),
 	setNumErrors(new Long($sumNumErrors.longValue())),
 	setSumLatency(new Long($sumSumLatency.longValue())),
 	setMinLatency(new Long($minMinLatency.longValue())),
 	setMaxLatency(new Long($maxMaxLatency.longValue())) 	
 }
end



// Note that only metrics that have changed will get impacted.
rule "PersistModifiedCompactNetworkMetricsConfigTypeStream"
   salience -500
when
 $metric : CompactNetworkMetrics() from entry-point "compactNetworkMetricsStream_ByConfigType"
then
 log.info("Marking CompactNetworkMetrics dirty " + $metric.getId() + " Output " + $metric.toString());
 dirtyCompactNetworkMetrics.add($metric);
end

/*
Need to see if the accumulate logic can be streamlined as mentioned in
http://docs.jboss.org/drools/release/5.2.0.CR1/droolsjbpm-introduction-docs/html/ch02.html Section 2.1.1.1 Multi-function Accumulates
rule "CompactNetworkMetricsCalculator"
   salience -200
   no-loop true
when
 $metric : CompactNetworkMetrics() from entry-point "compactNetworkMetricsStream" 
 accumulate (ClientNetworkMetrics(appId == $metric.appId, endMinute == $metric.endMinute))
 	 $sumNumSamples : sum($numSamples )
 	 $minLat : min($minLatency)


*/


